PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX fx:   <http://sparql.xyz/facade-x/ns/>
PREFIX xyz:  <http://sparql.xyz/facade-x/data/>
PREFIX ont: <http://w3id.org/polifonia/ontology/meetups#>
PREFIX meetup: <http://w3id.org/polifonia/pilot/meetups/>

CONSTRUCT{
?meetup a ont:MusicalMeetup ;
	ont:person ?personIRI ;
	ont:subject ?subjectIRI ;
	ont:place ?placeIRI ;
	ont:excerpt ?sentenceText ;
	ont:timeExpression ?time
} WHERE {

   SERVICE <x-sparql-anything:> {
	 BIND ( CONCAT ( "../extractedEntities/", ?_fileId , ".csv" ) AS ?location ) .
   	 fx:properties fx:location ?location ; fx:csv.headers "true" .
 {
 	 [] xyz:paragraphIndex ?paragraph ; 
 	    xyz:sentenceIndex ?sentence ; 
 	 	<http://sparql.xyz/facade-x/data/sentence> ?sentenceText
 		.
 } UNION{
	 [] xyz:paragraphIndex ?paragraph ; 
	    xyz:sentenceIndex ?sentence ; 
	 	<http://sparql.xyz/facade-x/data/URI> ?person ;
		xyz:entType "person"
		.
} UNION {
   	 [] xyz:paragraphIndex ?paragraph ; 
	    xyz:sentenceIndex ?sentence ; 
   	 	<http://sparql.xyz/facade-x/data/URI> ?place ;
   		xyz:entType "place"
		.
} UNION {
	 [] xyz:paragraphIndex ?paragraph ; 
   	    xyz:sentenceIndex ?sentence ; 
   	 	xyz:entity ?time ;
   		xyz:entType "time"
		.
}
   	 BIND ( IRI(?_subject) as ?subjectIRI ) .
	 BIND ( IRI(?person) as ?personIRI ) .
	 BIND ( IRI(?place) as ?placeIRI ) .
     BIND (fx:entity (meetup:, fx:String.toLowerCase(?_fileId), "/", ?paragraph, "/", ?sentence) AS ?meetup )
   }
}